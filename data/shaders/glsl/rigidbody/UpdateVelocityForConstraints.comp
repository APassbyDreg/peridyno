#version 450
#extension GL_GOOGLE_include_directive : enable
#include "SharedData.glsl"
#include "../math/Quat.glsl"

layout(std430, binding = 0) buffer VelocityOut {
	vec3 velocity[ ];
};

layout(std430, binding = 1) buffer AngularVelocityOut {
	vec3 angularVelocity[ ];
};

layout(std430, binding = 2) buffer AccPosIn {
	vec3 accPos[ ];
};

layout(std430, binding = 3) buffer AccQuatIn {
	vec4 accQuat[ ];
};

layout(std430, binding = 4) buffer QuatIn {
	vec4 quat[ ];
};

layout(std430, binding = 5) buffer BodyTypeIn {
	uint type[ ];
};

layout (binding = 6) uniform UBO {
	RigidBodySolverState state;
} params;

layout (push_constant) uniform PushConsts {
	uint bodyCount;
} pushConsts;

layout (local_size_x = 64) in;

void main() 
{
	uvec3 id = gl_GlobalInvocationID; 

	uint index = id.x;
	if (index >= pushConsts.bodyCount) 
		return;

	if(type[index] <= Kinematic) return;

    float dt = params.state.dt;
	float erp = params.state.erp;

	float linearDamping = erp;
	velocity[index] += linearDamping*accPos[index] / dt;

	float angularDamping = erp;
	vec4 dOmege = quat_mul(accQuat[index], quat_inverse(quat[index]))/dt;
	angularVelocity[index] += 2*angularDamping * dOmege.xyz;
}