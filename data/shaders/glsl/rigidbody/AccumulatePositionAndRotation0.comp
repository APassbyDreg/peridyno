#version 450
#extension GL_GOOGLE_include_directive : enable
#include "SharedData.glsl"
#include "../math/Quat.glsl"

layout(std430, binding = 0) buffer DPosOut {
	vec3 dpos[ ];
};

layout(std430, binding = 1) buffer DQuatOut {
	vec4 dQuat[ ];
};

layout(std430, binding = 2) buffer ConstraintPairIdIn {
	int id0[ ];
};

layout(std430, binding = 3) buffer ConstraintPairValIn {
	int val0[ ];
};

layout(std430, binding = 4) buffer CenterIn {
	vec3 center[ ];
};

layout(std430, binding = 5) buffer QuatIn {
	vec4 quat[ ];
};

layout(std430, binding = 6) buffer MassInvIn {
	float massInv[ ];
};

layout(std430, binding = 7) buffer inertiaWorldInvIn {
	mat3 inertiaWorldInv[ ];
};

layout(std430, binding = 8) buffer LambdaPositionIn {
	vec3 lambdaDeltaPosition[ ];
};

layout(std430, binding = 9) buffer LambdaAngularIn {
	vec3 lambdaDeltaAngular[ ];
};

layout(std430, binding = 10) buffer ConstraintsIn {
	ConstraintPair constraints[ ];
};

layout (push_constant) uniform PushConsts {
    uint constraintCount;
} pushConsts;

layout (local_size_x = 64) in;

void main() 
{
    uvec3 id = gl_GlobalInvocationID; 

	uint index = id.x;
	if (index >= pushConsts.constraintCount || id0[index] < 0) 
		return;


    if (index == 0 || id0[index] != id0[index-1])
    {
        int start_index = int(index);
        int bodyId = id0[index];

        vec3 center_i = center[bodyId];
        float massInv_i = massInv[bodyId];
        vec4 quat_i = quat[bodyId];
        mat3 inertiaWorldInv_i = inertiaWorldInv[bodyId];

        vec3 dp_i = vec3(0);
        vec4 dq_i = vec4(0);
        while(id0[start_index] == bodyId && start_index < pushConsts.constraintCount)
        {
            int cId = val0[start_index];

            vec3 p_i = lambdaDeltaPosition[cId];
            dp_i += p_i*massInv_i;
            dq_i += 0.5*quat_mul(vec4(inertiaWorldInv_i*cross(constraints[cId].r00.xyz, p_i), 0), quat_i);

            p_i = lambdaDeltaAngular[cId];
            dq_i += 0.5*quat_mul(vec4(inertiaWorldInv_i*p_i, 0), quat_i);

            start_index++;
        }

        dpos[bodyId] += dp_i;
        dQuat[bodyId] += dq_i;
    }
}