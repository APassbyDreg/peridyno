#version 450
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#include "SharedDataInFluid.glsl"

layout(std430, binding = 0) buffer VelUOut {
	float vel_u[ ];
};

layout(std430, binding = 1) buffer VelVOut {
	float vel_v[ ];
};

layout(std430, binding = 2) buffer VelWOut {
	float vel_w[ ];
};

layout(std430, binding = 3) buffer SDFIn {
	float boundarySDF[ ];
};

layout (binding = 4) uniform UBO {
	Array3DInfo arrayInfo;
} params;

layout (binding = 5) uniform GravityUBO {
	vec3 gravity;
};

layout (push_constant) uniform PushConsts {
    float dt;
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

void main() 
{
    uvec3 id = gl_GlobalInvocationID; 

    Array3DInfo aInfo = params.arrayInfo;
    uint i = id.x;
    uint j = id.y;
    uint k = id.z;

    if (!inside(i, j, k, aInfo)) 
		return;

    uint nx = aInfo.nx;
    uint ny = aInfo.ny;
    uint nz = aInfo.nz;

    Array3DInfo aUInfo = params.arrayInfo;  aUInfo.nx = nx+1;   aUInfo.nxy = aUInfo.nx * aUInfo.ny;
    Array3DInfo aVInfo = params.arrayInfo;  aVInfo.ny = ny+1;   aVInfo.nxy = aVInfo.nx * aVInfo.ny;
    Array3DInfo aWInfo = params.arrayInfo;  aWInfo.nz = nz+1;   aWInfo.nxy = aWInfo.nx * aWInfo.ny;

    uint k0 = INDEX(i, j, k, aInfo);
    
    if(i < nx - 1)
    {
        uint k1 = INDEX(i+1, j, k, aInfo);
        float s_mid = boundarySDF[k0] + boundarySDF[k1];
        if(s_mid < 0)
            vel_u[INDEX(i + 1, j, k, aUInfo)] += gravity.x*dt;
        else
            vel_u[INDEX(i + 1, j, k, aUInfo)] = 0;
    }

    if(j < ny - 1)
    {
        uint k1 = INDEX(i, j+1, k, aInfo);
        float s_mid = boundarySDF[k0] + boundarySDF[k1];
        if(s_mid < 0)
            vel_v[INDEX(i, j + 1, k, aVInfo)] += gravity.y*dt;
        else
            vel_v[INDEX(i, j + 1, k, aVInfo)] = 0;
    }
        
    if(k < nz - 1)
    {
        uint k1 = INDEX(i, j, k+1, aInfo);
        float s_mid = boundarySDF[k0] + boundarySDF[k1];
        if(s_mid < 0)
            vel_w[INDEX(i, j, k + 1, aWInfo)] += gravity.z*dt;
        else
            vel_w[INDEX(i, j, k + 1, aWInfo)] = 0;
    }
}