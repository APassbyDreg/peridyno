#version 450
#extension GL_GOOGLE_include_directive : enable
#include "SharedDataInFluid.glsl"

layout(std430, binding = 0) buffer DensityOut {
	float dst[ ];
};

layout(std430, binding = 1) buffer NormalIn {
	vec3 normals[ ];
};

layout(std430, binding = 2) buffer DensityIn {
	float src[ ];
};

layout(std430, binding = 3) buffer VelWIn {
	float vel_v[ ];
};

layout (binding = 4) uniform UBO {
	Array3DInfo arrayInfo;
} params;

layout (push_constant) uniform PushConsts {
    float dt;
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

void main() 
{
    uvec3 id = gl_GlobalInvocationID;

    Array3DInfo aInfo = params.arrayInfo;
    uint i = id.x;
    uint j = id.y;
    uint k = id.z;
	if (!inside(i, j, k, aInfo)) 
		return;

    uint nx = aInfo.nx;
    uint ny = aInfo.ny;
    uint nz = aInfo.nz;
    uint nxy = aInfo.nxy;

    Array3DInfo aVInfo = params.arrayInfo;  aVInfo.ny = ny+1;   aVInfo.nxy = aVInfo.nx * aVInfo.ny;

    uint j_plus = clamp(j + 1, 0, ny - 1);

    uint k0, k1;
    vec3 n0, n1;
    float c1, c0, dc;

    float ceo = FLUID_BETA/DEF_H;
    k0 = INDEX(i, j, k, aInfo);

    float weight = 0.0f;

    //j and j+1
    if (j < ny - 1)
    {
        k1 = INDEX(i, j_plus, k, aInfo);
 
        n0 = normals[k0];
        n1 = normals[k1];

        c1 = src[k1] * (1.0f - src[k1])*n1.y / DEF_OMEGA;
        c0 = src[k0] * (1.0f - src[k0])*n0.y / DEF_OMEGA;

        dc = 0.5f*(c1 + c0)*ceo*dt;

        weight = SharpeningWeight(vel_v[INDEX(i, j + 1, k, aVInfo)]*dt / DEF_H);
        dst[k1] += weight*dc;
    }
}